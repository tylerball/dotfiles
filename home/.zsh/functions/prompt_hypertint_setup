# vim:ft=zsh

function prompt_hypertint_precmd () {
    if (( $+functions[git-info] )); then
        git-info

        if ! is-true "$(git rev-parse --is-inside-work-tree 2> /dev/null)"; then
          return 1
        fi
        thaw="$(git log -2 --format="%s" 2> /dev/null | grep -E "WIP \[\w+\]")"
        if [[ -n "$thaw" ]]; then
          zstyle -s ':prezto:module:git:thaw' format 'thaw_format'
          zformat -f thaw_formatted "$thaw_format" "t:$thaw"
          git_info[thaw]="$thaw_formatted"
        else
          git_info[thaw]=$thaw
        fi
    fi

    if (( $+functions[python-info] )); then
        python-info
    fi

    if (( $+functions[ruby-info] )); then
        ruby-info
    fi
}

function prompt_hypertint_setup {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS

    prompt_opts=(cr percent subst)

    autoload -Uz add-zsh-hook

    add-zsh-hook precmd prompt_hypertint_precmd

    zstyle ':prezto:module:python:info:virtualenv' format '(%F{green}%v%f) :'

    zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
    zstyle ':prezto:prompt:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
    zstyle ':prezto:module:editor:info:keymap:alternate' format ' %F{yellow}❮%f%B%F{red}❮%f%b%F{red}❮%f'

    zstyle ':prezto:module:git:info' verbose 'yes'
    zstyle ':prezto:module:git:thaw' format '%F{195}❄️  frozen ❄️%f'
    zstyle ':prezto:module:git:info:action' format ' | %F{214}%s%f% '
    zstyle ':prezto:module:git:info:added' format ' %%B%F{green}●%f%%b'
    zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}⬆%f%%b'
    zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}⬇%f%%b'
    zstyle ':prezto:module:git:info:branch' format '%%B%F{4}%b%f'
    zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}●%f%%b'
    zstyle ':prezto:module:git:info:modified' format ' %%B%F{yellow}●%f%%b'
    zstyle ':prezto:module:git:info:renamed' format ' %%B%F{199}●%f%%b'
    zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}✭%f%%b'
    zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}═%f%%b'
    zstyle ':prezto:module:git:info:untracked' format ' %%B%F{blue}●%f%%b'
    zstyle ':prezto:module:git:info:commit' format '  %F{238}%.7c'
    zstyle ':prezto:module:git:info:keys' format \
        'prompt' '( %b%s %F{8})%a%d%m%r%u' \
        'rprompt' '%A%B%S%U%c'

    PROMPT='%F{red}${SSH_TTY:+[ %n@%m ] }%B%F{154}$python_info[virtualenv]%b%F{07}${PWD/#$HOME/~}%F{8} %B${git_info:+${(e)git_info[prompt]}}%b $git_info[thaw]
%(?.%F{238}❯%f.%B%F{124}❯%f%b) '
    RPROMPT='${editor_info[keymap]}${editor_info[overwrite]}%B%F{green}%V%f%b$ruby_info[version]${git_info[rprompt]} ${VIM:+" %B%F{green}V%f%b"}${git_info[commit]}%F{238}[ %F{252}%t %F{238}]'
    SPROMPT='zsh: correct %F{124}%R%f to %F{green}%r%f [nyae]? '
}

prompt_hypertint_setup "$@"

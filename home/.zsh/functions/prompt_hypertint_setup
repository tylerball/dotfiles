# vim:ft=zsh

function prompt_hypertint_precmd () {
  if (( $+functions[git-info] )); then
    git-info

    if ! is-true "$(git rev-parse --is-inside-work-tree 2> /dev/null)"; then
      return 1
    fi
    thaw="$(git log -2 --format="%s" 2> /dev/null | grep -E "WIP \[\w+\]")"
    if [[ -n "$thaw" ]]; then
      zstyle -s ':prezto:module:git:thaw' format 'thaw_format'
      zformat -f thaw_formatted "$thaw_format" "t:$thaw"
      git_info[thaw]="$thaw_formatted"
    else
      git_info[thaw]=$thaw
    fi
  fi
}

function prompt_hypertint_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_hypertint_precmd

  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{yellow}❮%F{16}❮%F{red}❮%f%b'
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:thaw' format '%F{195}❄️  frozen ❄️%f'
  zstyle ':prezto:module:git:info:action' format ' | %F{214}%s%f% '
  zstyle ':prezto:module:git:info:added' format ' %%B%F{green}●%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%%B%F{8}%b%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}●%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{yellow}●%f%%b'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}●%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{blue}●%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
      'prompt' '%b%s%a%d%m%r%u' \
      'rprompt' '%A%B  '

  PROMPT='%F{red}${SSH_TTY:+[ %n@%m ] }%f${PWD/#$HOME/~} ${git_info:+${(e)git_info[prompt]}} $git_info[thaw]
%(?.%F{238}❯%f.%B%F{124}❯%f%b) '
  RPROMPT='${editor_info[keymap]}${git_info[rprompt]}%F{8}%t'
  SPROMPT='zsh: correct %F{124}%R%f to %F{green}%r%f [nyae]? '
}

prompt_hypertint_setup "$@"
